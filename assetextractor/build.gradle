/*
 * assetextractor/build.gradle
 *
 * Copyright (C) 2020 Vilius Sutkus'89 <ViliusSutkus89@gmail.com>
 *
 * AssetExtractor-Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 3 as published by
 * the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

version = '1.2.2'
group = 'com.viliussutkus89'

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
android {
    compileSdkVersion 30
    buildToolsVersion "30.0.0"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30

        versionCode 9
        versionName project.version

        archivesBaseName = rootProject.name

        consumerProguardFiles 'proguard-rules.pro'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
}

configurations {
    javadocDependenciesToObtainClasspath
}

dependencies {
    implementation 'androidx.annotation:annotation:1.1.0'
    javadocDependenciesToObtainClasspath 'androidx.annotation:annotation:1.1.0'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}

task sourceJar(type: Jar) {
    into ("", { from android.sourceSets.main.java.srcDirs} )
    into ("", { from file("../LICENSE")})
    archiveClassifier = 'sources'
}

task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDependenciesToObtainClasspath
    title = "${rootProject.name} ${project.version} API"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['projectRelease']

    pkg {
        repo = 'maven-repo'
        name = rootProject.name
        desc = 'Android Asset Extractor'

        websiteUrl = 'https://github.com/ViliusSutkus89/AssetExtractor-Android'
        vcsUrl = 'https://github.com/ViliusSutkus89/AssetExtractor-Android.git'
        issueTrackerUrl = 'https://github.com/ViliusSutkus89/AssetExtractor-Android/issues'

        licenses = ['GPL-3.0-only']
        labels = ['asset', 'Android', 'extract']

        publish = true

        version {
            name = this.version
            released = new Date()
            vcsTag = "v${this.version}"
        }
    }
}

publishing{
    publications {
        projectRelease(MavenPublication) {
            artifactId rootProject.name
            artifact "$buildDir/outputs/aar/${rootProject.name}-release.aar"
            artifact sourceJar
            artifact javadocJar
            pom {
                packaging = 'aar'
                url = 'https://github.com/ViliusSutkus89/AssetExtractor-Android'
                inceptionYear = '2020'
                developers {
                    developer {
                        id = "ViliusSutkus89"
                        name = "Vilius Sutkus"
                        email = "ViliusSutkus89@gmail.com"
                    }
                }
                scm {
                    url = 'https://github.com/ViliusSutkus89/AssetExtractor-Android'
                    connection = 'https://github.com/ViliusSutkus89/AssetExtractor-Android.git'
                }
            }
            pom.withXml {
                def node = asNode()

                def licensesNode = node.appendNode("licenses")
                def thisProjLicense = licensesNode.appendNode('license')

                thisProjLicense.appendNode('name', 'AssetExtractor-Android')
                thisProjLicense.appendNode('url', "AssetExtractor-Android/LICENSE\n\n" + new File("$projectDir/../LICENSE").getText('UTF-8'))
                thisProjLicense.appendNode('distribution', 'repo')

                def dependenciesNode = node.appendNode('dependencies')
                // Iterate over the implementation dependencies (we don't want the test ones),
                // adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

task createEmptyFolderInTestAssetsForSpecificUnitTest {
    doLast {
        mkdir "$projectDir/src/androidTest/assets/emptyDirectory"
    }
}

tasks.whenTaskAdded {
    if (it.name == 'generateDebugAndroidTestAssets') {
        it.dependsOn createEmptyFolderInTestAssetsForSpecificUnitTest
    }
}